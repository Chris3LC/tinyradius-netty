plugins {
    id 'java-library'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.4"
}

javadoc {
    destinationDir file('docs')
    options.noTimestamp = true
    options.addBooleanOption('html5', true)  // comment out if building with <jdk9
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'grt-maven'
        name = 'tinyradius-netty'
        userOrg = 'globalreachtech'
        licenses = ['LGPL-2.1']
        vcsUrl = 'https://github.com/bintray/gradle-bintray-plugin.git'
        version {
            name = '1.0.1'
            released = new Date()
//            mavenCentralSync {
//                user = 'userToken' //OSS user token
//                password = 'paasword' //OSS user password
//            }

        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            afterEvaluate {
                artifact sourcesJar
                artifact javadocJar
            }
            groupId 'com.globalreachtech'
            artifactId 'tinyradius-netty'
            version '1.0.1'
        }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.26'
    implementation 'io.netty:netty-all:4.1.36.Final'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.4.2'
}
